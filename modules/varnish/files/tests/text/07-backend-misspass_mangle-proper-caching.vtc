varnishtest "backend misspass_mangle proper caching"

server s1 {
    # First miss (/api/rest_v1/)
    rxreq
    # Expect proper URL rewriting by misspass_mangle
    expect req.url == "/wikimedia.org/v1/media/math/render/svg/d494a1039a0708e25bfeb9abb059bbefdaf2bcc3"
    txresp

    # Second miss (X-Subdomain)
    rxreq
    expect req.http.Host == "test.org"
    txresp
} -start

varnish v1 -arg "-p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='exec gcc -std=gnu99 -g -O2 -fstack-protector-strong -Wformat -Werror=format-security -Wall -pthread -fpic -shared -Wl,-x -o %o %s'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "/usr/share/varnish/tests/wikimedia_misc-backend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-backend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "/usr/share/varnish/tests/wikimedia_text-backend.vcl";
} -start

client c1 {
    txreq -url "/api/rest_v1/media/math/render/svg/d494a1039a0708e25bfeb9abb059bbefdaf2bcc3" -hdr "Host: wikimedia.org"
    rxresp
    expect resp.http.X-Cache-Int ~ "miss"

    txreq -url "/api/rest_v1/media/math/render/svg/d494a1039a0708e25bfeb9abb059bbefdaf2bcc3" -hdr "Host: wikimedia.org"
    rxresp
    expect resp.http.X-Cache-Int ~ "hit"

    txreq -hdr "X-Subdomain: example.org" -hdr "x-dt-host: test.org"
    rxresp
    expect resp.http.X-Cache-Int ~ "miss"

    txreq -hdr "X-Subdomain: example.org" -hdr "x-dt-host: test.org"
    rxresp
    expect resp.http.X-Cache-Int ~ "hit"
} -run
