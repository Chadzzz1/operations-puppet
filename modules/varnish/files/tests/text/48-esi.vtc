# SPDX-License-Identifier: Apache-2.0
varnishtest "ESI"

server s1 {
    rxreq
    expect req.url == "/wiki/Main_Page"
    txresp -hdr "Content-Type: text/html; utf-8" -body {
        <html>
        <esi:include src="/esi"/>
        </html>
    }
    # /esi will be cached by v1 so only 1 request will hit s1
    rxreq
    expect req.url == "/esi"
    txresp -body {
        Included file
    }
    rxreq
    expect req.url == "/wiki/Private_Page"
    txresp -hdr "Cache-Control: private" -hdr "Content-Type: text/html; utf-8" -body {
        <html>
        <esi:include src="/esi"/>
        </html>
    }
} -start


varnish v1 -arg "-a ${tmpdir}/v1.sock -p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

    include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }
    sub vcl_deliver {
        set resp.http.url = req.url;
    }
    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";

} -start

client c1 -connect "${tmpdir}/v1.sock" {
    txreq -url "/wiki/Main_Page" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.body ~ "Included file"
    txreq -url "/wiki/Private_Page" -hdr "Cookie: enwikiSession=42;" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.body ~ "Included file"
} -run
