# SPDX-License-Identifier: Apache-2.0
varnishtest "Query parameter normalization"

server s1 -repeat 6 {
    rxreq
    txresp
} -start


varnish v1 -arg "-a ${tmpdir}/v1.sock -p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

    include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }
    sub vcl_deliver {
        set resp.http.url = req.url;
    }
    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";

} -start

client c1 -connect "${tmpdir}/v1.sock" {
    # enwiki requests don't get the querysort treatment yet.
    txreq -url "/w/index.php?title=Varnish&action=history" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.url == "/w/index.php?title=Varnish&action=history"

    # ...unless explicitly requested, via X-Wikimedia-Debug header.
    txreq -url "/w/index.php?title=Varnish&action=history" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "X-Wikimedia-Debug: querysort;"
    rxresp
    expect resp.http.url == "/w/index.php?action=history&title=Varnish"

    # testwiki requests get querysorted
    txreq -url "/w/index.php?title=Varnish&action=history" -hdr "Host: test.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.url == "/w/index.php?action=history&title=Varnish"

    # testwiki mobile requests get querysorted
    txreq -url "/w/index.php?title=Varnish&action=history" -hdr "Host: test.m.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.url == "/w/index.php?action=history&title=Varnish"

    # mediawikiwiki requests get querysorted
    txreq -url "/w/index.php?title=Squid&action=history" -hdr "Host: www.mediawiki.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.url == "/w/index.php?action=history&title=Squid"

    # mediawikiwiki mobile requests get querysorted
    txreq -url "/w/index.php?title=Squid&action=history" -hdr "Host: m.mediawiki.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.url == "/w/index.php?action=history&title=Squid"

} -run
