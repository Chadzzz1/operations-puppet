# SPDX-License-Identifier: Apache-2.0
varnishtest "request-target validation"

server s1 -repeat 5 {
    rxreq
    txresp
} -start


varnish v1 -arg "-a ${tmpdir}/v1.sock -p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

    include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }
    sub vcl_deliver {
        set resp.http.url = req.url;
    }
    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";

} -start

client c1 -connect "${tmpdir}/v1.sock" {
    # origin-form is accepted
    txreq -url "/wiki/Main_Page" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200
    txreq -url "/" -hdr "Host: www.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200
    # OPTIONS * should be allowed
    txreq -req OPTIONS -url "*" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "Origin: https://en.wikipedia.org"
    rxresp
    expect resp.status == 200
    # authority-form is accepted
    txreq -url "https://en.wikipedia.org/wiki/Main_Page" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200

    # other forms of request-target must be rejected
    txreq -url "wiki/Main_Page" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 400
} -run
