varnishtest "Test switching to cache_misc vcl"

server s1 {
} -start

varnish v1 -arg "-p cc_command='exec cc -fpic -shared -Wl,-x -L/usr/local/lib/ -o %o %s -lmaxminddb' -p vcc_allow_inline_c=true -p vcc_err_unref=false" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc-frontend" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc-frontend vcl1"

# Load cache_text VCL as usual
varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    sub vcl_recv {
        if (req.http.Host == "phab.wmfusercontent.org") {
            # Switch to misc-specific VCL
            return (vcl(wikimedia_misc-frontend));
        }
    }

    include "/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
}

varnish v1 -cliok "vcl.list"
varnish v1 -start
varnish v1 -cliok "vcl.list"

client c1 {
    # Trigger misc-specific behavior
    txreq -url "/file/data/ybcjrf2omnaelbfhxvsi/PHID-FILE-fqxtwmymtmrufi3riz2i/profile" -hdr "X-Client-IP: 93.184.216.34" -hdr "Host: phab.wmfusercontent.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 403

    # Trigger text-specific behavior
    txreq -url "/w/index.php?title=MyPage&action=info" -hdr "User-Agent: nintendo" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.location == "https://en.m.wikipedia.org/w/index.php?title=MyPage&action=info"
    expect resp.status == 302
} -run
