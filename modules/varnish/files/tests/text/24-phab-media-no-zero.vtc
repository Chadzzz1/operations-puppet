varnishtest "Forbid access to phab media content to zero users"

server s1 {
    rxreq
    txresp
    rxreq
    txresp
    rxreq
    txresp
    rxreq
    txresp
} -start

varnish v1 -arg "-p cc_command='exec gcc -std=gnu99 -g -O2 -fstack-protector-strong -Wformat -Werror=format-security -Wall -pthread -fpic -shared -Wl,-x -L/usr/local/lib/ -o %o %s -lmaxminddb' -p vcc_allow_inline_c=true -p vcc_err_unref=false" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
} -start

client c1 {
    # WP0 IP: phab media download
    txreq -url "/file/data/ybcjrf2omnaelbfhxvsi/PHID-FILE-fqxtwmymtmrufi3riz2i/profile" -hdr "X-Client-IP: 93.184.216.34" -hdr "Host: phab.wmfusercontent.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 403

    # WP0 IP: non-media GET request
    txreq -url "/file/potato/" -hdr "X-Client-IP: 93.184.216.34" -hdr "Host: phab.wmfusercontent.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200

    # non-WP0 IP: phab media download
    txreq -url "/file/data/ybcjrf2omnaelbfhxvsi/PHID-FILE-fqxtwmymtmrufi3riz2i/profile" -hdr "X-Client-IP: 192.0.2.42" -hdr "Host: phab.wmfusercontent.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200

    # WP0 IP: phab media upload
    txreq -req POST -url "/file/dropupload/?name=example.png" -hdr "X-Client-IP: 93.184.216.34" -hdr "Host: phab.wmfusercontent.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 403

    # WP0 IP: non-media upload
    txreq -req POST -url "/file/" -hdr "X-Client-IP: 93.184.216.34" -hdr "Host: phab.wmfusercontent.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200

    # non-WP0 IP: phab media upload
    txreq -req POST -url "/file/dropupload/?name=example.png" -hdr "X-Client-IP: 192.0.2.42" -hdr "Host: phab.wmfusercontent.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200
} -run
