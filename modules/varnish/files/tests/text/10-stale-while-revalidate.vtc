varnishtest "stale-while-revalidate"

server s1 {
    rxreq
    txresp -hdr "Cache-control: max-age = 2" -body "1111\n"

    rxreq
    txresp -hdr "Cache-control: max-age = 2" -body "22222\n"
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "${basepath}/usr/share/varnish/tests/wikimedia_misc-backend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-backend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_text-backend.vcl";
} -start

client c1 {
    # cache miss
    txreq
    rxresp
    expect resp.status == 200
    expect resp.bodylen == 5
    expect resp.http.X-Cache-Int ~ "miss"
    expect resp.http.Age == 0

    delay 1

    # cache hit
    txreq
    rxresp
    expect resp.status == 200
    expect resp.bodylen == 5
    expect resp.http.X-Cache-Int ~ "hit/1"
    expect resp.http.Age == 1
} -run

delay 3

client c1 {
    # stale object
    txreq
    rxresp
    expect resp.status == 200
    expect resp.bodylen == 5
    expect resp.http.X-Cache-Int ~ "hit/2"
    expect resp.http.Age == 4

    # fresh one
    txreq
    rxresp
    expect resp.status == 200
    expect resp.bodylen == 6

    # this is considered a cache hit because the actual object fetch happened
    # asynchronously in the background while we were in vcl_hit
    expect resp.http.X-Cache-Int ~ "hit/1"
    expect resp.http.Age == 0
} -run
